apply plugin: 'java'
apply plugin: "sonar-runner"
apply plugin: 'jacoco'

version = '1.0'

repositories {
	mavenCentral()
}

sourceSets {
	main {
		java {
			srcDir 'src/main/java'
		}
		resources {
			srcDir 'src/main/resources'
		}
	}
}

sourceSets {
	receiver {
		java {
			srcDir 'src/main/java'
		}
		resources {
			srcDir 'src/main/resources'
		}
	}
}

jacoco {
    toolVersion = "0.7.2.201409121644"
}

jacocoTestReport {
	description = "Generate Jacoco coverage reports after running tests."
    reports {
        xml {
            enabled true
            destination "${buildDir}/reports/jacoco/xml/jacoco.xml"
        }

        csv.enabled false

        html {
            enabled true
            destination "${buildDir}/reports/jacoco/html"
        }
    }
}

dependencies {
	compile files('libs/gson-2.2.4.jar')
	receiverCompile files('libs/gson-2.2.4.jar')
	testCompile group: 'junit', name: 'junit', version: '4.+'
}

task sourcesJar(type: Jar, dependsOn: classes) {
	classifier = 'sources'
	archiveName = jar.baseName +'-' + classifier + '.' + jar.extension
	from sourceSets.main.allSource
}

compileJava {
    options.fork = true
    options.forkOptions.executable = 'javac'
}

javadoc {
	configure(options) {
		links = ['http://docs.oracle.com/javase/7/docs/api/']
		bottom = 'Copyright &copy; 2014-2014 <a href=\"http://www.hbm.com\" target=\"_blank\">Hottinger Baldwin Messtechnik GmbH</a>.'
		footer = 'HBM Device Scan API'
		header = 'HBM Device Scan API'
		options.overview = "src/main/doc/overview.html"
	}
}

task javadocJar(type: Jar, dependsOn: javadoc) {
	classifier = 'javadoc'
	archiveName = jar.baseName +'-' + classifier + '.' + jar.extension
	from javadoc.destinationDir
}

artifacts {
	archives sourcesJar
	archives javadocJar
}

jar {
	archiveName = jar.baseName + '.' + jar.extension
	from configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
	exclude ('fakemessages.properties')
	manifest {
		attributes \
			'Implementation-Title': 'HBM Device Scan Library', \
			'Implementation-Version': version, \
			'Implementation-Vendor': 'Hottinger Baldwin Messtechnik GmbH', \
			'Built-By': System.getProperty('user.name'), \
			'Built-JDK': System.getProperty('java.version'), \
			'Build-Time': new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
	}
}

sonarRunner {
    toolVersion = '2.4' // default
    sonarProperties {
        property "sonar.host.url", "http://localhost:9000"
    }
}
